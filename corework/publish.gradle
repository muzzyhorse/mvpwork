apply plugin: 'maven-publish'

/**
 * 部署任务
 * = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
 * gradle deploy                     - 部署debug或release构建类型的包到seedland的nexus服务器
 * 部署仓库根据versionName动态选择，带有SNAPSHOT的版本会发布到snapshot仓库，其他发布到release仓库
 * = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
 * 作者：徐春蕾 时间：2017年12月7日
 */
task deploy(dependsOn: ':corework:clean') {

    def type = project.hasProperty('buildType') ? buildType : 'Release'
    doFirst {
        exec {
            commandLine "gradle", ":corework:assemble$type"
        }
    }

    doLast {
        exec {
            commandLine "gradle", "publish", "-PbuildType=$type"
        }
    }
}

publishing {
    publications {
        library(MavenPublication) {
            groupId 'cc.seedland.inf'
            artifactId 'corework'
            version corework_version_name

            def type = project.hasProperty('buildType') ? buildType : 'Release'
            artifact("$buildDir/outputs/aar/${project.getName()}-${type.toLowerCase()}.aar")

            pom.withXml {
                def node = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if(it.group != null && it.version != null) {
                        def dependencyNode = node.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }

        }
    }

    repositories{
        maven {
            if(corework_version_name.contains("SNAPSHOT")) {
                url "http://developer.seedland.cc/nexus/repository/maven-snapshots/"
            }else{
                url "http://developer.seedland.cc/nexus/repository/maven-releases/"
            }

            credentials {
                username "admin"
                password 'inf888'
            }
        }
    }
}